plugins {
    id 'java'
}

group 'org.oclc.hbase.analytics'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    withDeps
}

dependencies {
    implementation(project(':jmxtools'))
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    testImplementation(
            "org.slf4j:slf4j-log4j12:${slf4jVersion}",
            "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    )
    testImplementation("org.apache.hbase:hbase-client:${hbaseVersion}") {
        exclude group: 'org.apache.curator', module: '*'
    }

    withDeps(
            project(':jmxtools')
    )
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jar {
    dependsOn(':jmxtools:jar')
    manifest {
        attributes('Main-Class': 'org.oclc.hbase.analytics.jmx.jmx2file.Main')
    }

    from(configurations.withDeps.collect { it.isDirectory() ? it : zipTree(it) })
}

task demo(type: JavaExec) {
    main = "org.oclc.hbase.analytics.jmx.Jmx2File"
    classpath = sourceSets.test.runtimeClasspath
}

task dummy(type: JavaExec) {
    main = "org.oclc.hbase.analytics.jmx.Dummy"
    classpath = sourceSets.test.runtimeClasspath
}