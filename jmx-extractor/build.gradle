
plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
}

repositories {
    maven {
        url 'https://repository.cloudera.com/artifactory/cloudera-repos/'
    }
    maven {
        url 'https://artifact-m1.shr.oclc.org/artifactory/releases/'
    }
    maven {
        url 'https://artifact-m1.shr.oclc.org/artifactory/internal'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

version = '0.1'

configurations.all {
    resolutionStrategy {
        failOnDynamicVersions()
    }
}
configurations {
    baggage // defines additional classes to include in my jar!
}

dependencies {
    implementation(
            'org.slf4j:slf4j-log4j12:1.2.17',
            'org.slf4j:jcl-over-slf4j:1.7.5',
            'com.fasterxml.jackson.core:jackson-databind:2.11.3',
            'com.google.guava:guava:12.0.1'
    )
    implementation('org.apache.httpcomponents:fluent-hc:4.5.13') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation('org.apache.hbase:hbase-client:1.2.0-cdh5.16.2') {
        exclude group: 'org.apache.curator', module: '*'
    }

    // Use JUnit Jupiter API for testing.
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.6.2',
            'com.fasterxml.jackson.core:jackson-databind:2.11.3'
    )

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

    // special - include these in my jar!
    baggage('com.fasterxml.jackson.core:jackson-databind:2.11.3',
            'org.slf4j:jcl-over-slf4j:1.7.5'
    )
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

jar {
    manifest {
        attributes('Main-Class': 'org.oclc.hbase.tools.extractor.JmxPoll')
    }
    from(configurations.baggage.collect { it.isDirectory() ? it : zipTree(it) })
}

task demo(type: JavaExec) {
    main = "org.oclc.hbase.tools.extractor.job.JmxPoll"
    classpath = sourceSets.test.runtimeClasspath
}

task bulkdemo(type: JavaExec) {
    main = "org.oclc.hbase.tools.extractor.job.Poll4Bulk"
    classpath = sourceSets.test.runtimeClasspath
}
